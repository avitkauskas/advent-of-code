lines: Read\lines %../inputs/day01.txt

parse-line: fn { line } {
  n: line .rest .to-integer
  either line .first = "L" { negate n } { n } }

moves: lines .map { .parse-line }
start: 50

part1: fold\do moves 'acc [ start 0 ] 
  { ::m 
    [ ( acc .first + m ) % 100 ::new-pos 
      acc .second |when { new-pos .is-zero } { .inc } ] 
  } |second

count-zeros: fn { pos mov } {
  d: either mov .is-positive 
       { ( 100 - pos ) % 100 }
       { ( 100 + pos ) % 100 }
     |when { = 0 } { 100 }
  m: math/abs mov
  either m < d { 0 } { 1 + ( m - d ) // 100 }
}

part2: fold\do moves 'acc [ start 0 ]
  { ::m 
    [ ( acc .first + m ) % 100
      acc .second + count-zeros acc .first m ]
  } |second

print part1
print part2
